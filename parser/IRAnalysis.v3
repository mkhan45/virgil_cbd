type CBDType {
    case I32;
    case U32;
    case F32;
    case Bot;

    def toString() -> string {
        match (this) {
            I32 => return "i32";
            U32 => return "u32";
            F32 => return "f32";
            Bot => return "âŠ¥";
        }
    }
}

component CBDTypes {
    def intrinsic_type(fn_name: string) -> (Array<CBDType>, CBDType) {
        if (Strings.equal(fn_name, "readImmULEB32")) return ([], CBDType.U32);
        if (Strings.equal(fn_name, "readImmILEB32")) return ([], CBDType.I32);

        if (Strings.equal(fn_name, "pop_i32")) return ([], CBDType.I32);
        if (Strings.equal(fn_name, "pop_f32")) return ([], CBDType.F32);

        return (null, CBDType.Bot);
    }

    // should extract these from a file?
    def intrinsic_static(fn_name: string) -> (Array<bool>, bool) {
        if (Strings.equal(fn_name, "readImmULEB32")) return ([], true);
        if (Strings.equal(fn_name, "readImmILEB32")) return ([], true);

        if (Strings.equal(fn_name, "pop_i32")) return ([], false);
        if (Strings.equal(fn_name, "pop_f32")) return ([], false);
        if (Strings.equal(fn_name, "push_i32")) return ([false], false);
        if (Strings.equal(fn_name, "push_f32")) return ([false], false);

        if (Strings.equal(fn_name, "f_getLabel")) return ([true], true); 
        if (Strings.equal(fn_name, "f_getLocalType")) return ([true], true); 
        if (Strings.equal(fn_name, "f_isAtEnd")) return ([true], true); 

        if (Strings.equal(fn_name, "m_getGlobalType")) return ([true], true); 
        if (Strings.equal(fn_name, "m_getFieldKind")) return ([true, true], true); 
        if (Strings.equal(fn_name, "m_getFieldOffset")) return ([true, true], true); 
        if (Strings.equal(fn_name, "m_isMemory64")) return ([true], true); 
        if (Strings.equal(fn_name, "m_isTable64")) return ([true], true); 
        if (Strings.equal(fn_name, "m_getFuncSignature")) return ([true], true); 
        if (Strings.equal(fn_name, "m_getSignature")) return ([true], true); 

        if (Strings.equal(fn_name, "getLocal")) return ([true, true], false); 
        if (Strings.equal(fn_name, "getGlobal")) return ([true, true], false); 

        return (null, false);
    }
}

class EffectResult(stack_delta: int, ctl_xfer: bool) {}

component CBDEffects {
    def run_fx(fx: Vector<CBDEffect>) -> EffectResult {
        var sd = 0;
        var ctl = false;
        for (i < fx.length) {
            if (fx[i] == CBDEffect.Push) sd += 1;
            if (fx[i] == CBDEffect.Pop) sd -= 1;
            if (fx[i] == CBDEffect.CtlXfer) ctl = true;
        }
        return EffectResult.new(sd, ctl);
    }
}

type CBDEffect {
    // TODO: type push and pop?
    case Push;
    case Pop;
    case State;
    case CtlXfer;
    case None;

    def is_stack() -> bool {
        match (this) {
            Push => return true;
            Pop => return true;
            _ => return false;
        }
    }

    // this should probably be specific to tier
    def is_sidetable() -> bool {
        match (this) {
            CtlXfer => return true;
            _ => return false;
        }
    }

    def toString() -> string {
        match (this) {
            Push => return "push";
            Pop => return "pop";
            State => return "state";
            CtlXfer => return "ctl";
            None => return "none";
        }
    }
}

class SSADCtx {
    var body: SSAD;
    var static: HashMap<string, bool>;
    var types: HashMap<string, CBDType>;
    var effects: HashMap<string, CBDEffect>; // should it be a list?

    new(body) {
        static = HashMap.new(Strings.hash, Strings.equal);
        getStatic(body);

        types = HashMap.new(Strings.hash, Strings.equal);
        effects = HashMap.new(Strings.hash, Strings.equal);
        getTypes(body);
    }

    def getStatic(node: SSAD) {
        match (node) {
            LetApp(n, f, as, rest) => {
                if (Strings.equal(f, "id")) {
                    static[n] = static[as[0]];
                } else {
                    def f_static = CBDTypes.intrinsic_static(f);
                    def args_static = f_static.0;
                    def ret_static = f_static.1;

                    if (args_static != null)
                        for (i < as.length) static[as[i]] = args_static[i];
                    static[n] = ret_static;
                }

                getStatic(rest);
            }
            LetLit(n, l, t, rest) => {
                static[n] = true;
                getStatic(rest);
            }
            If(c, t, e, phis, rest) => {
                getStatic(t);
                getStatic(e);
                for (p in phis) static[p] = static[c];
                getStatic(rest);
            }
            Void => return;
            Injected(i, rest) => getStatic(rest);
            Final(v) => return;
        }
    }

    def getTypes(node: SSAD) {
        match (node) {
	        LetApp(n, f, as, rest) => {
                // don't bother checking args
                if (Strings.equal(f, "id")) {
                    types[n] = types[as[0]];
                } else {
                    def f_type = CBDTypes.intrinsic_type(f);
                    types[n] = f_type.1;
                }

                // could consider collecting effects from args
                effects[n] = CBDEffect.None;
                if (Strings.startsWith(f, "push")) effects[n] = CBDEffect.Push;
                if (Strings.startsWith(f, "pop")) effects[n] = CBDEffect.Pop;

                if (Strings.equal(f, "doBranch")) effects[n] = CBDEffect.CtlXfer;
                if (Strings.equal(f, "doFallThru")) effects[n] = CBDEffect.CtlXfer;
                if (Strings.equal(f, "doBlock")) effects[n] = CBDEffect.CtlXfer;
                if (Strings.equal(f, "doLoop")) effects[n] = CBDEffect.CtlXfer;
                if (Strings.equal(f, "doReturn")) effects[n] = CBDEffect.CtlXfer;
                if (Strings.equal(f, "doEnd")) effects[n] = CBDEffect.CtlXfer;
                if (Strings.equal(f, "doCall")) effects[n] = CBDEffect.CtlXfer;
                if (Strings.equal(f, "doTry")) effects[n] = CBDEffect.CtlXfer;
                if (Strings.equal(f, "trapUnreachable")) effects[n] = CBDEffect.CtlXfer;

                if (Strings.equal(f, "setLocal")) effects[n] = CBDEffect.State;
                if (Strings.equal(f, "setGlobal")) effects[n] = CBDEffect.State;
                if (Strings.equal(f, "mach_writeTable64")) effects[n] = CBDEffect.State;

                if (Strings.startsWith(f, "read")) effects[n] = CBDEffect.CtlXfer; // codeptr change ~= ctl xfer

                getTypes(rest);
            }
            LetLit(n, l, t, rest) => {
                effects[n] = CBDEffect.None;
                var tp: CBDType = CBDType.Bot;
                if (Strings.equal(t, "int")) tp = CBDType.I32;
                if (Strings.equal(t, "float")) tp = CBDType.F32;
                types[n] = tp;
                getTypes(rest);
            }
            If(c, t, e, phis, rest) => {
                // TODO: give phis types
                getTypes(t);
                getTypes(e);
                getTypes(rest);
            }
            Void => return;
            Injected(i, rest) => getStatic(rest);
            Final(v) => return;
        }
    }

    def collectEffects(node: SSAD, buf: Vector<CBDEffect>) {
        match (node) {
            Final(v) => return;
            Void => return;
            Injected(i, rest) => collectEffects(rest, buf);
            LetLit(n, l, t, rest) => collectEffects(rest, buf);
            LetApp(n, f, as, rest) => {
                if (effects[n] != CBDEffect.None) buf.put(effects[n]);
                collectEffects(rest, buf);
            }
            If(c, t, e, phis, rest) => {
                // TODO: t and e should have the same effects?
                collectEffects(t, buf);
                // collectEffects(e, buf);
                collectEffects(rest, buf);
            }
        }
    }

    def usesVar(node: SSAD, v: string) -> bool {
        match (node) {
            Void => return false;
            Final(n) => return Strings.equal(v, n);
            LetLit(_, _, _, rest) => return usesVar(rest, v);
            Injected(_, rest) => return usesVar(rest, v);
            LetApp(_, _, args, rest) => {
                for (i < args.length) if (Strings.equal(v, args[i])) return true;
                return usesVar(rest, v);
            }
            If(c, t, e, phis, rest) => return Strings.equal(v, c) || usesVar(t, v) || usesVar(e, v) || usesVar(rest, v);
        }
    }

    def DCE(node: SSAD) -> SSAD {
        match (node) {
            Void => return node;
            Final(_) => return node;
            Injected(inj, rest) => return SSAD.Injected(inj, DCE(rest));
            If(c, t, e, phis, rest) => return SSAD.If(c, DCE(t), DCE(e), phis, DCE(rest));
            LetLit(n, l, t, rest) => {
                def r = DCE(rest);
                def used = usesVar(r, n);
                return if(used, SSAD.LetLit(n, l, t, r), r);
            }
            LetApp(n, f, args, rest) => {
                def r = DCE(rest);
                def used = usesVar(r, n);
                def eff = effects[n] != CBDEffect.None;
                return if(used || eff, SSAD.LetApp(n, f, args, r), r);
            }
        }
    }

    def id_propagate(node: SSAD, substs: HashMap<string, string>) -> SSAD {
        match (node) {
            Void => return node;
            Final(x) => return if(substs.has(x), SSAD.Final(substs[x]), node);
            Injected(inj, rest) => return SSAD.Injected(inj, id_propagate(rest, substs));
            LetLit(n, l, t, rest) => return SSAD.LetLit(n, l, t, id_propagate(rest, substs));
            If(c, t, e, phis, rest) => {
                def new_c = if(substs.has(c), substs[c], c);
                def new_t = id_propagate(t, substs);
                def new_e = id_propagate(e, substs);
                def new_phis = phis; // TODO: idk, might want new substs?
                return SSAD.If(new_c, new_t, new_e, new_phis, id_propagate(rest, substs));
            }
            LetApp(n, f, args, rest) => {
                if (Strings.equal(f, "id")) {
                    var arg = args[0];
                    while (substs.has(arg)) arg = substs[arg]; // cycles should be impossible
                    substs[n] = arg;
                    return id_propagate(rest, substs);
                } else {
                    def new_args = Arrays.map(args, fun (a: string) => if(substs.has(a), substs[a], a));
                    return SSAD.LetApp(n, f, new_args, id_propagate(rest, substs));
                }
            }
        }
    }
}
