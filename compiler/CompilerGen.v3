class CBDDef(opcode: string, body: SSAD) {
    def buildString(buf: StringBuilder) {
        // TODO
    }
}

def is_newline(c: byte) -> bool {
    return c == '\n';
}

def find_idx(s: string, c: byte) -> int {
    for (i < s.length) if (s[i] == c) {
        return i;
    }

    return -1;
}

def extractInstrNames(v3_file: string) -> Array<string> {
    def defs = Vector<string>.new();

    // the .v3 file has the //@bytecode start comments, but we can only get
    // the s-expressions from the full file. To solve this, find the definition
    // names from the raw .v3 file, and then actually parse them from the s-expr file
    def lines = Strings.splitOnChar(v3_file, is_newline, -1);
    var cur_line = 0;
    while (!Strings.startsWith(lines[cur_line], "//@bytecode start")) cur_line += 1;

    while (!Strings.startsWith(lines[cur_line], "//@bytecode end")) {
        def line = Strings.strip(lines[cur_line]);
        if (GlobMatcher.new("def ?*()*{").matches(line)) {
            def fn_name_start = 4; // skip `def `
            def fn_name_end = find_idx(line, '(');

            def fn_name = Arrays.range(line, fn_name_start, fn_name_end);
            defs.put(fn_name);
            cur_line += 1;
        }

        cur_line += 1;
    }

    return defs.copy();
}

def isSpace(c: byte) -> bool {
    return c == ' ';
}

def hasMethod(reader: TextReader) -> bool {
    reader.skipWhitespace();
    return reader.optN("method") != -1;
}

def notLParen(c: byte) -> bool {
    return c != '(';
}

def readMethodName(reader: TextReader) -> string {
    reader.skipWhitespace();
    def end = reader.star_rel(1, notLParen);
    def tok = reader.readToken(end - reader.pos);
    return tok.image;
}

def nextLine(reader: TextReader) {
    def start_line = reader.line;
    while (reader.line == start_line) reader.advance1();
}

def containsStr(strs: Range<string>, k: string) -> bool {
    for (s in strs) if (Strings.equal(s, k)) return true;
    return false;
}

def runUnconditionally(node: SSAD, ctx: SSADCtx) -> bool {
    match (node) {
        LetApp(n, _, _, _) => return ctx.effects[n].is_sidetable();
        LetLit(n, _, _, _) => return ctx.effects[n].is_sidetable();
        If(_, _, _, _, _) => return true; // TODO: check phis?
        Void => return true;
        Injected(_, _) => return true;
        Final(_) => return true;
    }
}

def transformSSA(ssa: SSAD, ctx: SSADCtx) -> SSAD {
    match (ssa) {
        Void => return ssa;
        Final(_) => return ssa;
        Injected(inj, rest) => return SSAD.Injected(inj, transformSSA(rest, ctx));
        LetLit(n, l, t, rest) => {
            if (ctx.static[n]) {
                return SSAD.LetLit(n, l, t, transformSSA(rest, ctx));
            } else {
                // insert rtcast
                def fv = SSAD_IR.uidl("lit");
                ctx.static[fv] = true;
                ctx.types[fv] = CBDType.Bot; // doesn't really matter
                ctx.effects[fv] = CBDEffect.None;
                def rtcast_f = Strings.format1("rtcast_%s", ctx.types[n].toString());
                return SSAD.LetLit(fv, l, t,
                        SSAD.LetApp(n, rtcast_f, [fv], transformSSA(rest, ctx)));
            }
        }
        LetApp(n, f, args, rest) => {
            var res = SSAD.LetApp(n, f, args, transformSSA(rest, ctx));
            if (Strings.equal(f, "id")) {
                def old_static = ctx.static[args[0]];
                def new_static = ctx.static[n];
                if (old_static && !new_static && ctx.types[n] != CBDType.Bot) {
                    // transition from static to runtime
                    def rtcast_fn = Strings.format1("rtcast_%s", ctx.types[n].toString());
                    res = SSAD.LetApp(n, rtcast_fn, args, res.rest);
                }
            }

            if (Strings.equal(f, "==") ||  Strings.equal(f, "+") || Strings.equal(f, ">") || Strings.equal(f, "!=") || Strings.equal(f, "-")) {
                def tp = ctx.types[args[0]];
                def is_rt = !ctx.static[args[0]];
                var prefix: string;
                match (tp) {
                    I32 => prefix = if(is_rt, "EmitOpI32", "i32");
                    U32 => prefix = if(is_rt, "EmitOpU32", "u32");
                    F32 => prefix = if(is_rt, "EmitOpF32", "f32");
                    Bot => prefix = if(is_rt, "TypeVar", "Bot");
                }
                var op: string;
                if (Strings.equal(f, "==")) op = "equals";
                if (Strings.equal(f, "!=")) op = "not_equals";
                if (Strings.equal(f, "+")) op = "plus";
                if (Strings.equal(f, "-")) op = "minus";
                if (Strings.equal(f, ">")) op = "gt";

                def new_f = Strings.format2("%s.%s", prefix, op);
                ctx.static[n] = !is_rt;
                ctx.types[n] = tp;
                ctx.effects[n] = CBDEffect.None;

                res = SSAD.LetApp(n, new_f, args, res.rest);

                // detect when rhs is not runtime and insert an rtcast
                def rhs_rt = !ctx.static[args[1]];
                if (!rhs_rt) {
                    def fv = SSAD_IR.uidl("rtcast");
                    def rtcast_f = Strings.format1("rtcast_%s", tp.toString());
                    ctx.static[fv] = false;
                    ctx.types[fv] = ctx.types[args[1]];
                    ctx.effects[fv] = CBDEffect.None;
                    res = SSAD.LetApp(fv, rtcast_f, [args[1]],
                            SSAD.LetApp(n, new_f, [args[0], fv], res.rest));
                }
            }
            return res;
        }
        If(c, t, e, phis, rest) => {
            def tfx = Vector<CBDEffect>.new();
            def efx = Vector<CBDEffect>.new();
            ctx.collectEffects(t, tfx);
            ctx.collectEffects(e, efx);

            def t_res = CBDEffects.run_fx(tfx);
            def e_res = CBDEffects.run_fx(efx);
            // assert t_res.stack_delta == e_res.stack_delta

	    // TODO: somehow forward declare phis stack vars and then emit "pops" into the fwd declared phis
	    // which assign the phis to whichever branch ran
	    def emit_if_fn = if(ctx.static[c], "emitIfStatic", "emitIf");
	    return SSAD.LetApp(SSAD_IR.uidl("if"), emit_if_fn, [c],
			transformSSA(t, ctx).appended(
				SSAD.LetApp(SSAD_IR.uidl("else"), "emitElse", [],
					transformSSA(e, ctx).appended(
						SSAD.LetApp(SSAD_IR.uidl("endBrace"), "emitEnd", [],
							transformSSA(rest, ctx))))));
        }
    }
}

def extractDefs(instrs: Array<string>, filename: string, file_content: string) -> Array<CBDDef> {
    def defs: Vector<CBDDef> = Vector.new();
    def sexp_parser = SExprParser.new(filename, file_content);
    while (sexp_parser.ok && sexp_parser.pos < sexp_parser.data.length) {
        if (hasMethod(sexp_parser)) {
            def meth = readMethodName(sexp_parser);
            if (!containsStr(instrs, meth)) continue;
            nextLine(sexp_parser);
            def result = sexp_parser.readSExpr();
            match (result) {
                Success(sexp) => {
                    def stmt_res = VirgilStmts.from_sexpr(sexp);
                    match (stmt_res) {
                        Success(stmt) => {
                            def ir = SSAD_IR.from_virgil(stmt);
                            SSAD_IR.counter = 0;
                            def ssa = SSAD_IR.ssa(ir);
                            def analysis = SSADCtx.new(ssa);
                            def transformed = transformSSA(ssa, analysis);
                            def processed = analysis.DCE(analysis.id_propagate(transformed, HashMap<string, string>.new(Strings.hash, Strings.equal)));

                            def sb = StringBuilder.new();
                            def vars = Maps.keyList(analysis.static);
                            for (v = vars; v != null; v = v.tail) {
                                sb.puts("// ").puts(v.head).puts(" -- ");
                                sb.puts("static: ").puts(if(analysis.static[v.head], "yes", "no")).puts(", ");
                                sb.puts("type: ").puts(analysis.types[v.head].toString()).puts(", ");
                                sb.puts("effect: ").puts(analysis.effects[v.head].toString());
                                sb.ln();
                            }
                            def instSsa = SSAD.Injected(sb.toString(), processed);
                            defs.put(CBDDef.new(meth, instSsa));
                        }
                        _ => {
                            System.write(2, meth);
                            System.write(2, ": skipping unhandled stmt sexpr\n");
                            nextLine(sexp_parser);
                        }
                    }
                }
                _ => {
                    break; // TODO
                }
            }
        } else {
            nextLine(sexp_parser);
        }
    }

    return defs.copy();
}

def main(args: Array<string>) {
    def v3file = args[0];
    def v3bytes = System.fileLoad(v3file);
    if (v3bytes == null) {
        System.puts("Error reading ");
        System.puts(v3file);
        System.ln();
    }

    def sexpr_file = args[1];
    def sexpr_bytes = System.fileLoad(sexpr_file);
    if (sexpr_bytes == null) {
        System.puts("Error reading ");
        System.puts(sexpr_file);
        System.ln();
    }

    def instrs = extractInstrNames(v3bytes);
    def defs = extractDefs(instrs, sexpr_file, sexpr_bytes);

    // for concrete interpreter 

    def codestr_builder = StringBuilder.new();

    def dispatch_builder = StringBuilder.new();
    dispatch_builder.puts("def build_dispatch_table() -> Array<void -> void> {\n");
    dispatch_builder.puts("  DISPATCH_TABLE = Array.new(0xE5 + 1);\n"); // 0xE5 = highest opcode (SWITCH)

    for (d in defs) {
        codestr_builder.puts("def ");
        codestr_builder.puts(d.opcode);
        codestr_builder.puts("() ").puts("{\n");
        codestr_builder.puts(d.body.toString(1));
        codestr_builder.puts("}\n");

        dispatch_builder.puts("  DISPATCH_TABLE[Opcode.");
        dispatch_builder.puts(d.opcode);
        dispatch_builder.puts(".code] = ");
        dispatch_builder.puts(d.opcode);
        dispatch_builder.puts(";\n");
    }

    dispatch_builder.puts("  return DISPATCH_TABLE;\n");
    dispatch_builder.puts("}\n");

    def concrete_template = args[2];
    def concrete_bytes = System.fileLoad(concrete_template);
    if (concrete_bytes == null) {
        System.puts("Error reading ");
        System.puts(concrete_template);
        System.ln();
    }

    var first_newline = 0;
    while (first_newline < concrete_bytes.length) {
        if (concrete_bytes[first_newline] == '\n') break;
        first_newline += 1;
    }
    def template_code = Arrays.range(concrete_bytes, first_newline + 1, concrete_bytes.length);

    def final_code_builder = StringBuilder.new();
    final_code_builder.puts("// GENERATED BY ValidatorGen.v3\n");
    final_code_builder.puts(codestr_builder.toString());
    final_code_builder.puts(dispatch_builder.toString());
    final_code_builder.puts(template_code);

    System.puts(final_code_builder.toString());
}
